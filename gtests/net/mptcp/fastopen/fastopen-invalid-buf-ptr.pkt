// Test TCP fastopen behavior with NULL as buffer pointer, but a non-zero
// buffer length.
// Similar to tcp/syscall_bad_arg/fastopen-invalid-buf-ptr.pkt but for MPTCP
--tolerance_usecs=100000
`../common/defaults.sh`

// Cache warmup: send a Fast Open cookie request
 0.0    socket(..., SOCK_STREAM, IPPROTO_MPTCP) = 3
+0.0    fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0    setsockopt(3, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
+0.0    connect(3, ..., ...) = -1 EINPROGRESS (Operation is now in progress)

+0.01     >  S   0:0(0)                               <mss 1460, sackOK, TS val 100 ecr 0,   nop, wscale 8, FO,          nop, nop, mpcapable v1 flags[flag_h] nokey>
+0.01     <  S.  0:0(0)          ack 1     win 65535  <mss 1460, sackOK, TS val 700 ecr 100, nop, wscale 8, FO abcd1234, nop, nop, mpcapable v1 flags[flag_h] key[skey=2]>
+0.01     >   .  1:1(0)          ack 1                <nop, nop,         TS val 100 ecr 700,                                       mpcapable v1 flags[flag_h] key[ckey, skey]>

+0.01   close(3) = 0
+0.0      >   .  1:1(0)  ack 1             <nop, nop,         TS val 101 ecr 700, dss dack4=1 dsn8=1 ssn=0 dll=1 nocs fin, nop, nop>
+0.0      <   .  1:1(0)  ack 1  win 450    <nop, nop,         TS val 700 ecr 100, dss dack4=2 dsn4=1 ssn=0 dll=1 nocs fin, nop, nop>
+0.0      >   .  1:1(0)  ack 1             <nop, nop,         TS val 101 ecr 700, dss dack4=2 nocs>
+0.0      >  F.  1:1(0)  ack 1             <nop, nop,         TS val 101 ecr 700, dss dack4=2 nocs>
+0.0      <   .  1:1(0)  ack 2  win 450    <nop, nop,         TS val 700 ecr 100, dss dack4=2 nocs>

// reply with a small delay to let the kernel switching to a time-wait socket.
+0.2      <  F.  1:1(0)  ack 2  win 450    <nop, nop,         TS val 700 ecr 100, dss dack4=2 nocs>
+0.0      >   .  2:2(0)  ack 2             <nop, nop,         TS val 101 ecr 700>

// Test with MSG_FASTOPEN without TCP_FASTOPEN_CONNECT.
+0.0    socket(..., SOCK_STREAM, IPPROTO_MPTCP) = 4
+0.0    fcntl(4, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0    sendto(4, NULL, 1, MSG_FASTOPEN, ..., ...) = -1
+0.0    close(4) = 0

// Test with TCP_FASTOPEN_CONNECT without MSG_FASTOPEN.
+0.0    socket(..., SOCK_STREAM, IPPROTO_MPTCP) = 5
+0.0    fcntl(5, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0    setsockopt(5, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
+0.0    connect(5, ..., ...) = 0
+0.0    sendto(5, NULL, 1, 0, ..., ...) = -1
+0.0    close(5) = 0

// Test with both TCP_FASTOPEN_CONNECT and MSG_FASTOPEN.
+0.0    socket(..., SOCK_STREAM, IPPROTO_MPTCP) = 6
+0.0    fcntl(6, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0.0    setsockopt(6, SOL_TCP, TCP_FASTOPEN_CONNECT, [1], 4) = 0
+0.0    connect(6, ..., ...) = 0
+0.0    sendto(6, NULL, 1, MSG_FASTOPEN, ..., ...) = -1
+0.0    close(6) = 0
